/*
 * Copyright 2017 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

configurations {
  packagingOnly
}

dependencies {
  packagingOnly project(':cli')

  compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '16.12.0'
  compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
  compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
  compile group: 'commons-io', name: 'commons-io', version: '2.5'

  compile group: 'org.zeroturnaround', name: 'zt-exec', version: '1.9'

  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.1'

  testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
  testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'

  testCompile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.0.v20161208'
  testCompile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.0.v20161208'
  testCompile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit.http.server', version: '4.6.0.201612231935-r'

  testCompile group: 'org.apache.sshd', name: 'sshd-core', version: '1.3.0'
  testCompile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.56'

  testCompile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.22'
  testCompile group: 'org.apache.ant', name: 'ant', version: '1.10.0'
  testCompile group: 'org.zeroturnaround', name: 'zt-process-killer', version: '1.5'

  testCompile group: 'net.javacrumbs.json-unit', name: 'json-unit', version: '1.17.0'
  testCompile group: 'net.javacrumbs.json-unit', name: 'json-unit-fluent', version: '1.17.0'
}

def generatedResourcesOutput = file("src/main/resources-generated")
def generatedResourcesInput = file('src/main/resource-templates')

sourceSets {
  main {
    resources {
      output.dir generatedResourcesOutput, builtBy: 'generateResources'
    }
  }

  test {
    java {
      compileClasspath += configurations.compileOnly
      runtimeClasspath += configurations.compileOnly
    }
  }
}

task generateResources {
  inputs.dir(generatedResourcesInput)
  inputs.file(project.configurations.packagingOnly)
  outputs.dir(generatedResourcesOutput)

  doFirst {
    project.delete(generatedResourcesOutput)
    generatedResourcesOutput.mkdirs()

    project.copy {
      into generatedResourcesOutput
      from(generatedResourcesInput) {
        expand project.pluginDesc
      }
    }

    project.copy {
      into "${generatedResourcesOutput}/resources"
      from project.configurations.packagingOnly
      rename "(.*)-${project.version}.(.*)", '$1.$2'
    }
  }
}

jar {
  manifest {
    attributes(
      'ImplementationVersion': project.version,
      'Plugin-Revision': project.gitRevision,
      'Implementation-Title': project.name,
      'Implementation-Version': project.version,
      'Source-Compatibility': project.sourceCompatibility,
      'Target-Compatibility': project.targetCompatibility
    )
  }

  includeEmptyDirs false
  duplicatesStrategy 'exclude'

  project.artifacts.add 'archives', jar

  from(sourceSets.main.java) {
    into "/"
  }

  from({ project.configurations.compile }) {
    into "lib/"
  }

}
